apply plugin: 'com.android.library'

//gradle-bintray-plugin插件来上传到jcenter
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    //RxJava与RxAndroid
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    //注解
    implementation 'com.android.support:support-annotations:27.1.1'

    //AndroidUtilCode
    implementation 'com.blankj:utilcode:1.12.1'


}


//gradle-bintray-plugin插件来上传到jcenter
def siteUrl = 'https://github.com/lovingning/rregister'    // project homepage
def gitUrl = 'https://github.com/lovingning/rregister.git' // project git
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "rregister"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["GPL-3.0"]
        publish = true
    }
}

//版本
version = "0.0.1"
//包名
group = "com.knowledge.mnlin"

//生成jar包
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    options.links "https://github.com/lovingning/FastDev/tree/master/mylibrary/docs/javadoc"
    failOnError false
}
//javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//归档
artifacts {
    archives javadocJar
    archives sourcesJar
}

//生成pom文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'rregister'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'GNU General Public License, Version 3.0'
                        url 'http://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer { //填写的一些基本信息
                        id 'mnlin'
                        name '**.wang'
                        email 'mnlin0905@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

